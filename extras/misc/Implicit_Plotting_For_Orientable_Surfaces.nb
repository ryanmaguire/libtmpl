(* The goal is to parameterize every compact two-dimensional orientable       *)
(* manifold without boundary. Let's start with a means of parametrizing       *)
(* closed curves with "genus" n. The genus 1 curve is simple. 			      *)
ParametricPlot[
	{Cos[2 Pi t], Sin[2 Pi t]}, {t, 0, 1},
	AxesLabel->{"x", "y"}, PlotRange->All
]

(* All we've done is parameterize a circle, which we're calling a "genus 1    *)
(* closed curve" Now, if we imagine thickening this curve, we'd get a donut,  *)
(* or a torus. A torus is a genus 1 surface. So if we can formulate this idea *)
(* in terms of equations, then we've obtained our first compact orientable    *)
(* manifold of dimension two. By "thickening" we can say that, at each point  *)
(* on the circle, we attach a circle. 									      *)
ParametricPlot3D[
	{(2 + Cos[2 Pi t]) * Cos[2 Pi u], (2 + Cos[2 Pi t])Sin[2Pi u], Sin[2 Pi t]},
	{t, 0, 1}, {u, 0, 1}, AxesLabel->{"x", "y", "z"}, PlotRange->All
]

(* To get a "genus 2" closed curve, we can take the idea for the circle and   *)
(* double the speed in the y-component. What happens if we double t in the    *)
(* x-component? 														      *)
ParametricPlot[
	{Cos[2 Pi t], Sin[4 Pi t]}, {t, 0, 1},
	AxesLabel->{"x", "y"}, PlotRange->All
]

(* This is the lemniscate of Gerono. The equation he studied can be obtained  *)
(* from a few trigonometric formulas and manipulations. We want to study the  *)
(* solution set to: x^4 - x^2 + y^2 = 0. Let's show that this is the same     *)
(* curve. 																	  *)
ContourPlot[x^4 - x^2 + y^2 == 0, {x, -1, 1}, {y, -1, 1}]

(* With this we can now "implicitly" define a genus 2 surface. By implicitly, *)
(* I mean we can write the surface as the solution to f(x,y,z) = 0. 		  *)
(* Let's try this. 															  *)
h 				 := 0.05
lem[x_, y_]      := x^4 - x^2 + y^2
surf[x_, y_, z_] := lem[x, y]^2 + z^2 - h
ContourPlot3D[
	surf[x, y, z] == 0,
	{x, -1.2, 1.2}, {y, -1, 1}, {z, -1.2, 1.2},
	AxesLabel->{"x", "y", "z"}
]
(* We can try the same thing with (cos(t), sin(3t)) as follows:			      *)
ParametricPlot[
	{Cos[2 Pi t], Sin[6 Pi t]}, {t, 0, 1},
	AxesLabel->{"x", "y"}, PlotRange->All
]

lem3[x_,y_]       := y^2 + 6x^2(1 - x^2)^2 - 9x^4(1 - x^2) - (1 - x^2)^3
surf3[x_, y_, z_] := lem3[x, y]^2 + z^2 - h
ContourPlot[lem3[x,y] == 0, {x, -1, 1}, {y, -1, 1}]
ContourPlot3D[
	surf3[x,y,z] == 0,
	{x, -1.5, 1.5}, {y, -1.3, 1.3}, {z, -1.2, 1.2},
	AxesLabel->{"x", "y", "z"}, PlotPoints->100
]
